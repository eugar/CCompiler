program -> declList
declList -> declList decl
declList -> decl
decl -> funcDecl
decl -> varDecl
funcDecl -> typeSpec ID ( params ) compStmt
typeSpec -> returnTypeSpec
returnTypeSpec -> int
returnTypeSpec -> char
returnTypeSpec -> bool
params -> paramList
params -> EMPTY
paramList -> paramList , typeSpec ID
paramList -> typeSpec ID
stmt -> exprStmt
stmt -> compStmt
stmt -> selStmt
stmt -> iterStmt
stmt -> retStmt
stmt -> breakStmt
stmt -> varDecl
compStmt -> { stmtList }
stmtList -> stmtList stmt
stmtList -> stmt
stmtList -> EMPTY
exprStmt -> expr ;
exprStmt -> ;
selStmt -> ifStmt
selStmt -> ifStmt elseStmt
ifStmt -> if ( simpleExpr ) stmt
elseStmt -> else stmt
elseStmt -> else ifStmt
elseStmt -> else ifStmt elseStmt
iterStmt -> while ( simpleExpr ) stmt
retStmt -> return ;
retStmt -> return expr ;
breakStmt -> break ;
expr -> mutable mutBinOp expr
expr -> mutable mutUnaryOp
expr -> mutUnaryOp mutable
expr -> simpleExpr
mutBinOp -> =
mutBinOp -> +=
mutBinOp -> -=
mutBinOp -> *
mutBinOp -> /
mutUnaryOp -> ++
mutUnaryOp -> --
simpleExpr -> simpleExpr || andExpr
simpleExpr -> andExpr
andExpr -> andExpr && unaryRelExpr
andExpr -> unaryRelExpr
unaryRelExpr -> ! unaryRelExpr
unaryRelExpr -> relExpr
relExpr -> sumExpr relOp sumExpr
relExpr -> sumExpr
relOp -> <=
relOp -> <
relOp -> >
relOp -> >=
relOp -> =
relOp -> !=
sumExpr -> term sumOp sumExpr
sumExpr -> term
sumOp -> +
sumOp -> -
term -> term mulOp unaryExpr
term -> unaryExpr
mulOp -> *
mulOp -> /
mulOp -> %
unaryExpr -> unaryOp mutable
unaryExpr -> immutable
unaryExpr -> mutable
unaryOp -> -
unaryOp -> &
mutable -> ID
immutable -> ( expr )
immutable -> call
immutable -> constant
call -> ID ( args )
args -> argList
args -> EMPTY
argList -> argList , expr
argList -> expr
constant -> NUMCONST
constant -> CHARCONST
constant -> true
constant -> false
varDecl -> typeSpec varDeclList
varDeclList -> varDeclList , varDeclInit
varDeclList -> varDeclInit
varDeclInit -> ID ;
varDeclInit -> ID = simpleExpr ;
